buildscript {
    ext {
        corda_release_group = 'net.corda'
        corda_release_version = '4.1'
        corda_gradle_plugins_version = '4.0.45'
        junit_version = '4.12'
        quasar_version = '0.7.11_r3'
        spring_boot_version = '2.0.4.RELEASE'
        spring_boot_gradle_plugin_version = '2.0.4.RELEASE'
        slf4j_version = '1.7.25'
        log4j_version = '2.11.2'

        corda_platform_version = '4'
        corbeans_version = '0.26'
        corda_solutions_version = '2.0'

        tokens_release_version = '1.0'
        tokens_release_group = 'com.r3.corda.lib.tokens'

        accounts_release_group = 'com.r3.corda.lib.accounts'
        accounts_release_version = '1.0-RC01'
        business_netowork_membership_release = '2.0'
    }


    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda' }
        maven { url 'http://ci-artifactory.corda.r3cev.com/artifactory/corda-lib-dev' }
        maven { url 'http://ci-artifactory.corda.r3cev.com/artifactory/corda-lib' }
        flatDir {
            dirs 'libs'
        }
    }

    dependencies {
        classpath "net.corda.plugins:cordapp:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:cordformation:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:quasar-utils:$corda_gradle_plugins_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_gradle_plugin_version"
    }
}

allprojects {
    apply plugin: 'java'

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda' }
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-lib' }
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-lib-dev' }
        maven { url 'https://jitpack.io' }
        maven { url "https://repo.gradle.org/gradle/libs-releases-local" }
        maven {
            url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-solutions-releases'
        }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-parameters" // Required by Corda's serialisation framework.
    }

    jar {
        // This makes the JAR's SHA-256 hash repeatable.
        preserveFileTimestamps = false
        reproducibleFileOrder = true
        // Don't include log config files.
        exclude "**/log4j2*.xml"
        // We need this because log4j2.xml is pulled in from multiple dependency JARs.
        // JAR signing will fail if there are multiple files with the same name.
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}


apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'net.corda.plugins.cordformation'
apply plugin: 'net.corda.plugins.quasar-utils'

cordapp {
    info {
        name "Land Investment Network CorDapp"
        vendor "OneConnect"
        targetPlatformVersion corda_platform_version.toInteger()
        minimumPlatformVersion corda_platform_version.toInteger()
    }
}

sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
    }
}

dependencies {
    testCompile "junit:junit:$junit_version"
    implementation fileTree(dir: 'lib', include: ['*.jar'])
    // For logging.
    testCompile "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_version}"
    testCompile "org.apache.logging.log4j:log4j-web:${log4j_version}"
    // Corda dependencies.
    cordaCompile "$corda_release_group:corda-core:$corda_release_version"
    cordaCompile "$corda_release_group:corda-node-api:$corda_release_version"
    cordaRuntime "$corda_release_group:corda:$corda_release_version"

    // CorDapp dependencies.
    cordapp project(":workflows")
    cordapp project(":contracts")

    // Accounts Library dependencies
    cordapp "$accounts_release_group:accounts-contracts:$accounts_release_version"
    cordapp "$accounts_release_group:accounts-flows:$accounts_release_version"

    // Token SDK dependencies.
    cordapp "$tokens_release_group:tokens-contracts:$tokens_release_version"
    cordapp "$tokens_release_group:tokens-workflows:$tokens_release_version"
    cordapp "$tokens_release_group:tokens-money:$tokens_release_version"

    cordaCompile "$accounts_release_group:accounts-contracts:$accounts_release_version"
    cordaCompile "$accounts_release_group:accounts-flows:$accounts_release_version"

    // CorDapp dependencies.
    cordapp "$corda_release_group:corda-finance-contracts:$corda_release_version"
    cordapp "$corda_release_group:corda-finance-workflows:$corda_release_version"
    cordapp "$corda_release_group:corda-confidential-identities:$corda_release_version"

    // Logging
    cordaCompile "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_version}"
    cordaCompile "org.apache.logging.log4j:log4j-web:${log4j_version}"
    cordaCompile "org.slf4j:jul-to-slf4j:$slf4j_version"

}

task deployNodes(type: net.corda.plugins.Cordform, dependsOn: ['jar']) {
    nodeDefaults {
        projectCordapp {
            deploy = false
        }
        cordapp project(':contracts')
        cordapp project(':workflows')

        cordapp("$tokens_release_group:tokens-contracts:$tokens_release_version")
        cordapp("$tokens_release_group:tokens-workflows:$tokens_release_version")
        cordapp("$tokens_release_group:tokens-money:$tokens_release_version")

        cordapp("$accounts_release_group:accounts-contracts:$accounts_release_version")
        cordapp("$accounts_release_group:accounts-flows:$accounts_release_version")

        cordapp("$corda_release_group:corda-finance-contracts:$corda_release_version")
        cordapp("$corda_release_group:corda-finance-workflows:$corda_release_version")
        cordapp("$corda_release_group:corda-confidential-identities:$corda_release_version")

        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]    }
    node {
        name "O=Notary,L=Pretoria,C=ZA"
        notary = [validating : false]
        p2pPort 10021
        rpcSettings {
            address("localhost:10022")
            adminAddress("localhost:10023")
        }
    }

    node {
        name "O=DeptLandAffairs,L=Pretoria,C=ZA"
        p2pPort 10024
        rpcSettings {
            address("localhost:10025")
            adminAddress("localhost:10026")
        }

    }
    node {
        name "O=Bank,L=Johannesburg,C=ZA"
        p2pPort 10027
        rpcSettings {
            address("localhost:10028")
            adminAddress("localhost:10029")
        }

    }
    node {
        name "O=LipNetworkOperator,L=Johannesburg,C=ZA"
        p2pPort 10030
        rpcSettings {
            address("localhost:10031")
            adminAddress("localhost:10032")
        }

    }

    node {
        name "O=Regulator,L=Johannesburg,C=ZA"
        p2pPort 10033
        rpcSettings {
            address("localhost:10034")
            adminAddress("localhost:10035")
        }

    }

}

task installQuasar(type: Copy) {
    destinationDir rootProject.file("lib")
    from(configurations.quasar) {
        rename 'quasar-core(.*).jar', 'quasar.jar'
    }
}

